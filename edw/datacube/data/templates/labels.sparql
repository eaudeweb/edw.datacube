PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dad-prop: <http://semantic.digital-agenda-data.eu/def/property/>
SELECT DISTINCT ?uri ?notation ?label ?short_label
                ?group_notation ?order (xsd:string(?inner_orderraw) as ?inner_order) WHERE {

  ?uri
    skos:notation ?notation ;
    skos:prefLabel ?label .
  FILTER(LANG(?label) = "" || LANGMATCHES(LANG(?label), "en"))
  OPTIONAL {
    ?uri skos:altLabel ?short_label .
    FILTER(LANG(?short_label) = "" || LANGMATCHES(LANG(?short_label), "en"))
  }
  OPTIONAL {
    ?uri dad-prop:membership ?group .
    ?group dad-prop:member-of [skos:notation ?group_notation].
    OPTIONAL {?group dad-prop:order ?inner_orderraw}
  }
  OPTIONAL {
    ?uri dad-prop:order ?order .
  }
  FILTER (
  {%- for uri in uri_list %}
    ?uri = {{ uri|uri_n3 }}
    {%- if not loop.last %} || {% endif %}
  {%- endfor %}
  )
}
LIMIT 1000
